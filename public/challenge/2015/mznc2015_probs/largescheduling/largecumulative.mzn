%------------------------------------------------------------------------------%
% A simple benchmark to test the scalability 
% of propagators for the cumulative constraint 
% for large sets of tasks with large durations.
% Instances randomly generated by Pierre Schaus
% and used in a paper submitted at CP2015.
%------------------------------------------------------------------------------%
% Includes

include "cumulative.mzn";

%------------------------------------------------------------------------------%
% Parameters

int: capa;                          % Resource capacity

int: n_tasks;                       % Number of tasks
set of int: tasks = 1..n_tasks;     % Set of tasks

array[tasks] of int: d;             % Duration of tasks
array[tasks] of int: r;             % Resource demand of tasks

int: horizon = sum(i in tasks)(d[i]);   % Trivial upper bound on the horizon

%------------------------------------------------------------------------------%
% Variables

array[tasks] of var 0..horizon: s;
var 0..horizon: objective = max(i in tasks)(s[i] + d[i]);

%------------------------------------------------------------------------------%
% Constraints

constraint cumulative(s, d, r, capa);

constraint redundant_constraint(
    forall(i in tasks)(
        s[i] + d[i] <= objective
    )
);

%------------------------------------------------------------------------------%
% Search and solve item

solve 
    :: int_search(s, input_order, indomain_min, complete) 
    minimize objective;

%------------------------------------------------------------------------------%
% Output item

output [
    "objective = ", show(objective), ";\n",
    "s = ", show(s), ";\n"
];

%------------------------------------------------------------------------------%

